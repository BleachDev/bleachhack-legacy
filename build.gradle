plugins {
	id 'maven-publish'
	id 'fabric-loom' version "0.10-SNAPSHOT"
}

sourceCompatibility = JavaVersion.VERSION_16
targetCompatibility = JavaVersion.VERSION_16

archivesBaseName = project.archives_base_name
version = project.mod_version
group = project.maven_group

repositories {
	maven {
		name = 'legacy-fabric'
		url = 'https://maven.legacyfabric.net'
	}
	
	// custom mapping maven because yarn devs are bozos
	maven {
		name = 'bleachhack'
		url = 'https://bleachhack.org/maven'
	}
	
	maven {
		url "https://jitpack.io"
	}
}

//minecraft {
//	intermediaryUrl = {
//		return "https://maven.legacyfabric.net/net/fabricmc/intermediary/" + it + "/intermediary-" + it + "-v2.jar";
//	}
//}

loom {
// Uncomment when using loom 0.10
	setIntermediaryUrl('https://maven.legacyfabric.net/net/fabricmc/intermediary/%1$s/intermediary-%1$s-v2.jar');
}

def lwjglVersion = System.getProperty("os.name").toLowerCase().contains("mac") ? "2.9.1" : "2.9.0"

dependencies {
	implementation 'org.apache.logging.log4j:log4j-core:2.17.0'
	implementation 'org.apache.logging.log4j:log4j-api:2.17.0'

	implementation "org.lwjgl.lwjgl:lwjgl_util:${lwjglVersion}"
	implementation "org.lwjgl.lwjgl:lwjgl:${lwjglVersion}"
	implementation "org.lwjgl.lwjgl:lwjgl-platform:${lwjglVersion}"

	minecraft "com.mojang:minecraft:${project.minecraft_version}"
	mappings "net.fabricmc:yarn:${project.yarn_mappings}:v2"
	modImplementation "net.fabricmc:fabric-loader:${project.loader_version}"
	
	include(modImplementation("com.github.fzakaria:ascii85:1.2"))
	include(modImplementation("com.github.jagrosh:DiscordIPC:-SNAPSHOT"))
}

configurations.all {
	resolutionStrategy {
		dependencySubstitution {
			substitute module('org.lwjgl.lwjgl:lwjgl_util:2.9.1-nightly-20130708-debug3') with module("org.lwjgl.lwjgl:lwjgl_util:${lwjglVersion}")
			substitute module('org.lwjgl.lwjgl:lwjgl:2.9.1-nightly-20130708-debug3') with module("org.lwjgl.lwjgl:lwjgl:${lwjglVersion}")
		}
		force "org.lwjgl.lwjgl:lwjgl-platform:${lwjglVersion}"
	}
}

processResources {
	inputs.property "version", project.version

	filesMatching("fabric.mod.json") {
		expand "version": project.version
	}
}

// ensure that the encoding is set to UTF-8, no matter what the system default is
// this fixes some edge cases with special characters not displaying correctly
// see http://yodaconditions.net/blog/fix-for-java-file-encoding-problems-with-gradle.html
tasks.withType(JavaCompile).configureEach {
	it.options.encoding = "UTF-8"
}

java {
	// Loom will automatically attach sourcesJar to a RemapSourcesJar task and to the "build" task
	// if it is present.
	// If you remove this line, sources will not be generated.
	withSourcesJar()
}

jar {
	from("LICENSE") {
		rename { "${it}_${project.archivesBaseName}"}
	}
}

// configure the maven publication
publishing {
	publications {
		mavenJava(MavenPublication) {
			from components.java
		}
	}

	// select the repositories you want to publish to
	repositories {
		// uncomment to publish to the local maven
		// mavenLocal()
	}
}
